cmake_minimum_required(VERSION 3.19)

project(LDPC4QKDLibrary)

get_filename_component(LDPC4QKD_SRC_DIR ${CMAKE_CURRENT_LIST_DIR} DIRECTORY)
get_filename_component(LDPC4QKD_TESTS_DIR "${LDPC4QKD_SRC_DIR}/tests" ABSOLUTE)

if (NOT EXISTS "${CMAKE_CURRENT_LIST_DIR}/autogen_ldpc_matrix_csc.hpp")
    message(WARNING "C++ code containing the LDPC matrix not found.
    Use the Julia scripts to automatically generate it form a .cscmat file! A minimal example will be generated automatically.")

    add_custom_command(
            OUTPUT autogen_ldpc_matrix_csc.hpp
            DEPENDS "${LDPC4QKD_TESTS_DIR}/fortest_autogen_ldpc_matrix_csc.hpp"
            COMMAND ${CMAKE_COMMAND} -E copy
            "${LDPC4QKD_TESTS_DIR}/fortest_autogen_ldpc_matrix_csc.hpp"
            "${CMAKE_CURRENT_LIST_DIR}/autogen_ldpc_matrix_csc.hpp"
            VERBATIM
    )
endif (NOT EXISTS "${CMAKE_CURRENT_LIST_DIR}/autogen_ldpc_matrix_csc.hpp")


if (NOT EXISTS "${CMAKE_CURRENT_LIST_DIR}/autogen_rate_adaption.hpp")
    message(WARNING "C++ code containing the rate adaption for the LDPC matrix not found.
    Use Julia to automatically generate it form an appropriate CSV file! A minimal example will be generated automatically.")

    add_custom_command(
            OUTPUT autogen_rate_adaption.hpp
            DEPENDS "${LDPC4QKD_TESTS_DIR}/fortest_autogen_rate_adaption.hpp"
            COMMAND ${CMAKE_COMMAND} -E copy
            "${LDPC4QKD_TESTS_DIR}/fortest_autogen_rate_adaption.hpp"
            "${CMAKE_CURRENT_LIST_DIR}/autogen_rate_adaption.hpp"
            VERBATIM
    )
endif (NOT EXISTS "${CMAKE_CURRENT_LIST_DIR}/autogen_rate_adaption.hpp")


add_library(LDPC4QKD INTERFACE
        rate_adaptive_code.hpp  # contains decoder and encoder. Does not need any other files to work.

        encoder.hpp # contains only minimal encoder that needs static storage of the LDPC matrix
        autogen_ldpc_matrix_csc.hpp # static storage of the LDPC matrix. Automatically generated by Julia script.
        autogen_rate_adaption.hpp # static storage of the rate adaption specification. Automatically generated by Julia script.
        read_scsmat_format.hpp # helper methods to generate static storage (not needed to use encoder/decoder class).
        )

target_compile_features(LDPC4QKD INTERFACE cxx_std_17)

target_include_directories(LDPC4QKD
        INTERFACE
        ${CMAKE_CURRENT_LIST_DIR}
        )

# Create alias with "namespace". This is the target to be used to link to the library in higher level projects.
add_library(LDPC4QKD::LDPC4QKD ALIAS LDPC4QKD)
